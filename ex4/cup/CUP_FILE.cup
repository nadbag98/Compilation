/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import java.io.PrintWriter;
import java.io.*;
import AST.*;

parser code 
{:
	public Lexer lexer;
	String path;
	public int prevLine = 0;

	public Parser(Lexer lexer, String path)
	{
		super(lexer);
		this.lexer = lexer;
		this.path = path;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print("] ");	
		try {
		PrintWriter file_writer = new PrintWriter(this.path);
		file_writer.print("ERROR(");
		file_writer.print(lexer.getLine());
		file_writer.print(")");
		file_writer.close(); 
		} catch (Exception e){
		System.out.print(this.path);
		}
		System.exit(0);
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	prevLine = lexer.getLine();
	Symbol s;
	s = lexer.next_token();
	//System.out.print(s.sym);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	
	if (s.sym == TokenNames.INT){
		if (Integer.parseInt(s.value.toString()) >= 32768){
			PrintWriter file_writer = new PrintWriter(path);
			file_writer.print("ERROR");
			file_writer.close();
			System.exit(0);
		}		
	}
					
	
	if (s.value != null)
	{
		System.out.print("(");
		System.out.print(s.value);
		System.out.print(")");
	}
	
	if (s.sym == TokenNames.ERROR){
		PrintWriter file_writer = new PrintWriter(path);
		file_writer.print("ERROR");
		file_writer.close();
		System.exit(0);
	}
	
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal NIL;
terminal COMMA;
terminal TYPE_INT;
terminal TYPE_VOID;
terminal LT;
terminal GT;
terminal ARRAY;
terminal CLASS;
terminal EXTENDS;
terminal RETURN;
terminal NEW;
terminal TYPE_STRING;
terminal ERROR;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_PRO Program;
non terminal AST_DEC dec;
non terminal AST_TYPE type;
non terminal AST_ARRAY arrayTypedef;
non terminal AST_VARDEC varDec;
non terminal AST_FUNCDEC funcDec;
non terminal AST_CLASSDEC classDec;
non terminal AST_EXP exp;
non terminal AST_VAR var;
non terminal AST_STMT stmt;
non terminal AST_NEWEXP newExp;
non terminal AST_CFIELD cField;
non terminal AST_STMT_LIST stmtList;
non terminal AST_TYPE_LIST typeIdList;
non terminal AST_CFIELD_LIST cFieldList;
non terminal AST_EXP_LIST expList;
non terminal AST_INITIAL initial;
non terminal AST_VARDEC_CLASS varDecClass;
non terminal AST_CLASS_DUMMY classDummy;

/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left ASSIGN;
precedence left EQ;
precedence left LT, GT;
precedence left PLUS, MINUS;
precedence left TIMES, DIVIDE;
precedence left LBRACK;
precedence left LPAREN;
precedence left DOT;

/************************/
/* START WITH: stmtList */
/************************/
start with initial;

/********************/
/* DERIVATION RULES */
/********************/

initial		::=		Program:p 	{: RESULT = new AST_INITIAL(p, prevLine); :} 
					;

Program		::=		dec:d 		{: RESULT = new AST_PRO(d, null, prevLine); :}
					| dec:d Program:p 	{: RESULT = new AST_PRO(d, p, prevLine); :}
					;

dec			::=		varDec:v 	{: RESULT = new AST_DEC(v, 1, prevLine); :}
					| funcDec:f 	{: RESULT = new AST_DEC(f, 2, prevLine); :}
					| classDec:c	{: RESULT = new AST_DEC(c, 3, prevLine); :}
					| arrayTypedef:a 	{: RESULT = new AST_DEC(a, 4, prevLine); :}
					;

type 		::=		TYPE_INT 	{: RESULT = new AST_TYPE("int", prevLine); :}
					| TYPE_STRING 	{: RESULT = new AST_TYPE("string", prevLine); :}
					| TYPE_VOID 	{: RESULT = new AST_TYPE("void", prevLine); :}
					| ID:i 	{: RESULT = new AST_TYPE(i, prevLine); :}
					;

arrayTypedef ::= 	ARRAY ID:i EQ type:t LBRACK RBRACK SEMICOLON	{: RESULT = new AST_ARRAY(i, t, prevLine); :}
					;

varDec		::=		type:t ID:i SEMICOLON 	{: RESULT = new AST_VARDEC(t, i, null, 0, prevLine); :}
					| type:t ID:i ASSIGN exp:e SEMICOLON 	{: RESULT = new AST_VARDEC(t, i, e, 0, prevLine); :}
					| type:t ID:i ASSIGN newExp:n SEMICOLON 	{: RESULT = new AST_VARDEC(t, i, n, 1, prevLine); :}
					;
					
varDecClass		::=		type:t ID:i SEMICOLON 	{: RESULT = new AST_VARDEC_CLASS(t, i, null, 0, 0, prevLine); :}
					| type:t ID:i ASSIGN STRING:s SEMICOLON 	{: RESULT = new AST_VARDEC_CLASS(t, i, s, 0, 1, prevLine); :}
					| type:t ID:i ASSIGN INT:i2 SEMICOLON 	{: RESULT = new AST_VARDEC_CLASS(t, i, null, i2, 2, prevLine); :}
					| type:t ID:i ASSIGN MINUS INT:i2 SEMICOLON 	{: RESULT = new AST_VARDEC_CLASS(t, i, null, -i2, 2, prevLine); :}
					| type:t ID:i ASSIGN NIL SEMICOLON 	{: RESULT = new AST_VARDEC_CLASS(t, i, null, 0, 3, prevLine); :}
					;

stmtList	::=		stmt:s	stmtList:l		{: RESULT = new AST_STMT_LIST(s, l, prevLine); :}
					| stmt:s				{: RESULT = new AST_STMT_LIST(s, null, prevLine); :}
					;

typeIdList	::= 	type:t ID:i 	{: RESULT = new AST_TYPE_LIST(t, null, i, lexer.getLine()); :}
					| 	type:t ID:i COMMA typeIdList:l	{: RESULT = new AST_TYPE_LIST(t, l, i, prevLine); :}
					;


funcDec		::= 	type:t ID:i LPAREN RPAREN LBRACE stmtList:l RBRACE	{: RESULT = new AST_FUNCDEC(t, i, null, l, prevLine); :}
					|	type:t ID:i LPAREN typeIdList:l1 RPAREN LBRACE stmtList:l2 RBRACE	{: RESULT = new AST_FUNCDEC(t, i, l1, l2, prevLine); :}	
					;

cFieldList	::=		cField:c	cFieldList:l		{: RESULT = new AST_CFIELD_LIST(c, l, prevLine); :}
					| cField:c				{: RESULT = new AST_CFIELD_LIST(c, null, prevLine); :}
					;
					
classDummy	::= 	CLASS 		{: RESULT = new AST_CLASS_DUMMY(prevLine); :}
					;

classDec 	::= 	classDummy:cd ID:i LBRACE cFieldList:c RBRACE 	{: RESULT = new AST_CLASSDEC(cd, i, null, c, prevLine); :}
					| classDummy:cd ID:i1  EXTENDS ID:i2 LBRACE cFieldList:c RBRACE 	{: RESULT = new AST_CLASSDEC(cd, i1, i2, c, prevLine); :}
					;

expList		::= 	exp:e 	{: RESULT = new AST_EXP_LIST(e, null, prevLine);:}
					| 	exp:e COMMA expList:l	{: RESULT = new AST_EXP_LIST(e, l, prevLine);:}
					;

exp 		::= 	var:v 	{: RESULT = new AST_EXP_VAR(v, null, null, prevLine);    :}
					| LPAREN exp:e RPAREN	{: RESULT = new AST_EXP_EXP(e, null, null, prevLine);    :}
					| exp:e1 PLUS exp:e2 	{: RESULT = new AST_EXP_EXP(e1, new AST_BINOP(1, prevLine), e2, prevLine);   :}
					| exp:e1 MINUS exp:e2 	{: RESULT = new AST_EXP_EXP(e1, new AST_BINOP(2, prevLine), e2, prevLine);   :}
					| exp:e1 TIMES exp:e2 	{: RESULT = new AST_EXP_EXP(e1, new AST_BINOP(3, prevLine), e2, prevLine);   :}
					| exp:e1 DIVIDE exp:e2 	{: RESULT = new AST_EXP_EXP(e1, new AST_BINOP(4, prevLine), e2, prevLine);   :}
					| exp:e1 LT exp:e2 	{: RESULT = new AST_EXP_EXP(e1, new AST_BINOP(5, prevLine), e2, prevLine);   :}
					| exp:e1 GT exp:e2 	{: RESULT = new AST_EXP_EXP(e1, new AST_BINOP(6, prevLine), e2, prevLine);   :}
					| exp:e1 EQ exp:e2 	{: RESULT = new AST_EXP_EXP(e1, new AST_BINOP(7, prevLine), e2, prevLine);   :}					
					| ID:i LPAREN RPAREN 	{: RESULT = new AST_EXP_ID(i, null, prevLine);    :}
					| ID:i LPAREN expList:l RPAREN 	{: RESULT = new AST_EXP_ID(i, l, prevLine);     :}
					| var:v DOT ID:i LPAREN RPAREN 	{: RESULT = new AST_EXP_VAR(v, i, null, prevLine);    :}
					| var:v DOT ID:i LPAREN expList:l RPAREN 	{: RESULT = new AST_EXP_VAR(v, i, l, prevLine);    :}
					| INT:i 	{: RESULT = new AST_EXP_INT(i,0, prevLine);    :}
					| MINUS INT:i 	{: RESULT = new AST_EXP_INT(i,1, prevLine);    :}
					| NIL 	{: RESULT = new AST_EXP_REST(null, prevLine);    :} 
					| STRING:s 	{: RESULT = new AST_EXP_REST(s, prevLine);    :}
					;

var 		::= 	ID:i 	{: RESULT = new AST_VAR(null, i, null, prevLine);   :}
					| var:v DOT ID:i 	{: RESULT = new AST_VAR(v, i, null, prevLine);    :}
					| var:v LBRACK exp:e RBRACK {: RESULT = new AST_VAR(v, null, e, prevLine);    :}
					;
					
stmt 		::= 	varDec:v 	{: RESULT = new AST_STMT_VARDEC(v, prevLine);    :}
					| var:v ASSIGN exp:e SEMICOLON 	{: RESULT = new AST_STMT_VAR_EXP(v, e, null, prevLine);    :}
					| var:v ASSIGN newExp:n SEMICOLON 	{: RESULT = new AST_STMT_VAR_EXP(v, null, n, prevLine);    :}
					| RETURN SEMICOLON 	{: RESULT = new AST_STMT_ID(null, null, prevLine);   :}
					| RETURN exp:e SEMICOLON 	{: RESULT = new AST_STMT_EXP(e, null, 0, prevLine);     :}
					| IF LPAREN exp:e RPAREN LBRACE stmtList:l RBRACE 	{: RESULT = new AST_STMT_EXP(e, l, 0, prevLine);    :}
					| WHILE LPAREN exp:e RPAREN LBRACE stmtList:l RBRACE 	{: RESULT = new AST_STMT_EXP(e, l, 1, prevLine);    :}
					| ID:i LPAREN RPAREN SEMICOLON	{: RESULT = new AST_STMT_ID(i, null, prevLine);     :}
					| var:v DOT ID:i LPAREN RPAREN SEMICOLON	{: RESULT = new AST_STMT_VAR_ID(v, i, null, prevLine);    :}
					| ID:i LPAREN expList:l RPAREN SEMICOLON	{: RESULT = new AST_STMT_ID(i, l, prevLine);     :}
					| var:v DOT ID:i LPAREN expList:e RPAREN SEMICOLON	{: RESULT = new AST_STMT_VAR_ID(v, i, e, prevLine);    :}
					;

newExp		::= 	NEW type:t 	{: RESULT = new AST_NEWEXP(t, null, prevLine);    :}
					| NEW type:t LBRACK exp:e RBRACK 	{: RESULT = new AST_NEWEXP(t, e, prevLine);    :}
					;

cField 		::= 	varDecClass:v 	{: RESULT = new AST_CFIELD(v, null, prevLine);    :}
					| funcDec:f 	{: RESULT = new AST_CFIELD(null, f, prevLine);    :}
					;
