/*************/
/* IMPORT(S) */
/*************/
import java_cup.runtime.*;
import java.io.PrintWriter;
import java.io.*;
import AST.*;

parser code 
{:
	public Lexer lexer;
	String path;

	public Parser(Lexer lexer, String path)
	{
		super(lexer);
		this.lexer = lexer;
		this.path = path;
	}
	public void report_error(String message, Object info)
	{
		System.out.print("ERROR >> ");		
		System.out.print("[");
		System.out.print(lexer.getLine());
		System.out.print("] ");	
		try {
		PrintWriter file_writer = new PrintWriter(this.path);
		file_writer.print("ERROR(");
		file_writer.print(lexer.getLine());
		file_writer.print(")");
		file_writer.close(); 
		} catch (Exception e){
		System.out.print(this.path);
		}
		System.exit(0);
	}
:}

/************************/
/* CUSTOMIZE CUP PARSER */
/************************/
scan with
{:
	Symbol s;
	s = lexer.next_token();
	//System.out.print(s.sym);
	System.out.print("[");
	System.out.print(lexer.getLine());
	System.out.print("] ");
	System.out.print(TokenNames.terminalNames[s.sym]);
	
	if (s.sym == TokenNames.INT){
		if (Integer.parseInt(s.value.toString()) >= 32768){
			PrintWriter file_writer = new PrintWriter(path);
			file_writer.print("ERROR");
			file_writer.close();
			System.exit(0);
		}		
	}
					
	
	if (s.value != null)
	{
		System.out.print("(");
		System.out.print(s.value);
		System.out.print(")");
	}
	
	if (s.sym == TokenNames.ERROR){
		PrintWriter file_writer = new PrintWriter(path);
		file_writer.print("ERROR");
		file_writer.close();
		System.exit(0);
	}
	
	System.out.print("\n");
	return s; 
:};

/*************/
/* TERMINALS */
/*************/
terminal IF;
terminal EQ;
terminal DOT;
terminal PLUS;
terminal MINUS;
terminal TIMES;
terminal WHILE;
terminal DIVIDE;
terminal LPAREN;
terminal RPAREN;
terminal LBRACK;
terminal RBRACK;
terminal LBRACE;
terminal RBRACE;
terminal SEMICOLON;
terminal ASSIGN;
terminal NIL;
terminal COMMA;
terminal TYPE_INT;
terminal TYPE_VOID;
terminal LT;
terminal GT;
terminal ARRAY;
terminal CLASS;
terminal EXTENDS;
terminal RETURN;
terminal NEW;
terminal TYPE_STRING;
terminal ERROR;

/*************/
/* TERMINALS */
/*************/
terminal Integer INT;
terminal String ID;
terminal String STRING;

/*****************/
/* NON TERMINALS */
/*****************/
non terminal AST_PRO Program;
non terminal AST_DEC dec;
non terminal AST_TYPE type;
non terminal AST_ARRAY arrayTypedef;
non terminal AST_VARDEC varDec;
non terminal AST_FUNCDEC funcDec;
non terminal AST_CLASSDEC classDec;
non terminal AST_EXP exp;
non terminal AST_VAR var;
non terminal AST_STMT stmt;
non terminal AST_NEWEXP newExp;
non terminal AST_CFIELD cField;
non terminal AST_BINOP binop;
non terminal AST_STMT_LIST stmtList;
non terminal AST_TYPE_LIST typeIdList;
non terminal AST_CFIELD_LIST cFieldList;
non terminal AST_EXP_LIST expList;
non terminal AST_INITIAL initial;

/***********************/
/* OPERATOR PRECEDENCE */
/***********************/
precedence left ASSIGN;
precedence left EQ;
precedence left LT;
precedence left GT;
precedence left PLUS;
precedence left MINUS;
precedence left TIMES;
precedence left DIVIDE;
precedence left LBRACK;
precedence left LPAREN;
precedence left DOT;

/************************/
/* START WITH: stmtList */
/************************/
start with initial;

/********************/
/* DERIVATION RULES */
/********************/

initial		::=		Program:p 	{: RESULT = new AST_INITIAL(p, lexer.getLine()); :} 
					;

Program		::=		dec:d 		{: RESULT = new AST_PRO(d, null, lexer.getLine()); :}
					| dec:d Program:p 	{: RESULT = new AST_PRO(d, p, lexer.getLine()); :}
					;

dec			::=		varDec:v 	{: RESULT = new AST_DEC(v, 1, lexer.getLine()); :}
					| funcDec:f 	{: RESULT = new AST_DEC(f, 2, lexer.getLine()); :}
					| classDec:c	{: RESULT = new AST_DEC(c, 3, lexer.getLine()); :}
					| arrayTypedef:a 	{: RESULT = new AST_DEC(a, 4, lexer.getLine()); :}
					;

type 		::=		TYPE_INT 	{: RESULT = new AST_TYPE("int", lexer.getLine()); :}
					| TYPE_STRING 	{: RESULT = new AST_TYPE("string", lexer.getLine()); :}
					| TYPE_VOID 	{: RESULT = new AST_TYPE("void", lexer.getLine()); :}
					| ID:i 	{: RESULT = new AST_TYPE(i, lexer.getLine()); :}
					;

arrayTypedef ::= 	ARRAY ID:i EQ type:t LBRACK RBRACK SEMICOLON	{: RESULT = new AST_ARRAY(i, t, lexer.getLine()); :}
					;

varDec		::=		type:t ID:i SEMICOLON 	{: RESULT = new AST_VARDEC(t, i, null, 0, lexer.getLine()); :}
					| type:t ID:i ASSIGN exp:e SEMICOLON 	{: RESULT = new AST_VARDEC(t, i, e, 0, lexer.getLine()); :}
					| type:t ID:i ASSIGN newExp:n SEMICOLON 	{: RESULT = new AST_VARDEC(t, i, n, 1, lexer.getLine()); :}
					;

stmtList	::=		stmt:s	stmtList:l		{: RESULT = new AST_STMT_LIST(s, l, lexer.getLine()); :}
					| stmt:s				{: RESULT = new AST_STMT_LIST(s, null, lexer.getLine()); :}
					;

typeIdList	::= 	type:t ID:i 	{: RESULT = new AST_TYPE_LIST(t, null, i, lexer.getLine()); :}
					| 	type:t ID:i COMMA typeIdList:l	{: RESULT = new AST_TYPE_LIST(t, l, i, lexer.getLine()); :}
					;


funcDec		::= 	type:t ID:i LPAREN RPAREN LBRACE stmtList:l RBRACE	{: RESULT = new AST_FUNCDEC(t, i, null, l, lexer.getLine()); :}
					|	type:t ID:i LPAREN typeIdList:l1 RPAREN LBRACE stmtList:l2 RBRACE	{: RESULT = new AST_FUNCDEC(t, i, l1, l2, lexer.getLine()); :}	
					;

cFieldList	::=		cField:c	cFieldList:l		{: RESULT = new AST_CFIELD_LIST(c, l, lexer.getLine()); :}
					| cField:c				{: RESULT = new AST_CFIELD_LIST(c, null, lexer.getLine()); :}
					;

classDec 	::= 	CLASS ID:i LBRACE cFieldList:c RBRACE 	{: RESULT = new AST_CLASSDEC(i, null, c, lexer.getLine()); :}
					| CLASS ID:i1  EXTENDS ID:i2 LBRACE cFieldList:c RBRACE 	{: RESULT = new AST_CLASSDEC(i1, i2, c, lexer.getLine()); :}
					;

expList		::= 	exp:e 	{: RESULT = new AST_EXP_LIST(e, null, lexer.getLine());:}
					| 	exp:e COMMA expList:l	{: RESULT = new AST_EXP_LIST(e, l, lexer.getLine());:}
					;

exp 		::= 	var:v 	{: RESULT = new AST_EXP_VAR(v, null, null, lexer.getLine());    :}
					| LPAREN exp:e RPAREN	{: RESULT = new AST_EXP_EXP(e, null, null, lexer.getLine());    :}
					| exp:e1 binop:b exp:e2 	{: RESULT = new AST_EXP_EXP(e1, b, e2, lexer.getLine());   :}
					| ID:i LPAREN RPAREN 	{: RESULT = new AST_EXP_ID(i, null, lexer.getLine());    :}
					| ID:i LPAREN expList:l RPAREN 	{: RESULT = new AST_EXP_ID(i, l, lexer.getLine());     :}
					| var:v DOT ID:i LPAREN RPAREN 	{: RESULT = new AST_EXP_VAR(v, i, null, lexer.getLine());    :}
					| var:v DOT ID:i LPAREN expList:l RPAREN 	{: RESULT = new AST_EXP_VAR(v, i, l, lexer.getLine());    :}
					| INT:i 	{: RESULT = new AST_EXP_INT(i,0, lexer.getLine());    :}
					| MINUS INT:i 	{: RESULT = new AST_EXP_INT(i,1, lexer.getLine());    :}
					| NIL 	{: RESULT = new AST_EXP_REST(null, lexer.getLine());    :} 
					| STRING:s 	{: RESULT = new AST_EXP_REST(s, lexer.getLine());    :}
					;

var 		::= 	ID:i 	{: RESULT = new AST_VAR(null, i, null, lexer.getLine());   :}
					| var:v DOT ID:i 	{: RESULT = new AST_VAR(v, i, null, lexer.getLine());    :}
					| var:v LBRACK exp:e RBRACK {: RESULT = new AST_VAR(v, null, e, lexer.getLine());    :}
					;
					
stmt 		::= 	varDec:v 	{: RESULT = new AST_STMT_VARDEC(v, lexer.getLine());    :}
					| var:v ASSIGN exp:e SEMICOLON 	{: RESULT = new AST_STMT_VAR_EXP(v, e, null, lexer.getLine());    :}
					| var:v ASSIGN newExp:n SEMICOLON 	{: RESULT = new AST_STMT_VAR_EXP(v, null, n, lexer.getLine());    :}
					| RETURN SEMICOLON 	{: RESULT = new AST_STMT_ID(null, null, lexer.getLine());   :}
					| RETURN exp:e SEMICOLON 	{: RESULT = new AST_STMT_EXP(e, null, 0, lexer.getLine());     :}
					| IF LPAREN exp:e RPAREN LBRACE stmtList:l RBRACE 	{: RESULT = new AST_STMT_EXP(e, l, 0, lexer.getLine());    :}
					| WHILE LPAREN exp:e RPAREN LBRACE stmtList:l RBRACE 	{: RESULT = new AST_STMT_EXP(e, l, 1, lexer.getLine());    :}
					| ID:i LPAREN RPAREN SEMICOLON	{: RESULT = new AST_STMT_ID(i, null, lexer.getLine());     :}
					| var:v DOT ID:i LPAREN RPAREN SEMICOLON	{: RESULT = new AST_STMT_VAR_ID(v, i, null, lexer.getLine());    :}
					| ID:i LPAREN expList:l RPAREN SEMICOLON	{: RESULT = new AST_STMT_ID(i, l, lexer.getLine());     :}
					| var:v DOT ID:i LPAREN expList:e RPAREN SEMICOLON	{: RESULT = new AST_STMT_VAR_ID(v, i, e, lexer.getLine());    :}
					;

newExp		::= 	NEW type:t 	{: RESULT = new AST_NEWEXP(t, null, lexer.getLine());    :}
					| NEW type:t LBRACK exp:e RBRACK 	{: RESULT = new AST_NEWEXP(t, e, lexer.getLine());    :}
					;

cField 		::= 	varDec:v 	{: RESULT = new AST_CFIELD(v, null, lexer.getLine());    :}
					| funcDec:f 	{: RESULT = new AST_CFIELD(null, f, lexer.getLine());    :}
					;

binop 		::= 	PLUS	{: RESULT = new AST_BINOP(1, lexer.getLine());    :}
					| MINUS 	{: RESULT = new AST_BINOP(2, lexer.getLine());     :}
					| TIMES 	{: RESULT = new AST_BINOP(3, lexer.getLine());     :}
					| DIVIDE 	{: RESULT = new AST_BINOP(4, lexer.getLine());     :}
					| LT 	{: RESULT = new AST_BINOP(5, lexer.getLine());     :}
					| GT 	{: RESULT = new AST_BINOP(6, lexer.getLine());     :}
					| EQ 	{: RESULT = new AST_BINOP(7, lexer.getLine());     :}
					;

